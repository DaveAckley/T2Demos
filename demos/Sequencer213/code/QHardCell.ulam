
use HC3NgbState;

transient QHardCellState : Intention + DTU + QDebugUtils {
  HC3State mHC3State;
  HC3State & getHC3State() { return mHC3State; }

  Bool mIsBlocked;              // update returned 'block()'
  
  Bool block() {
    mIsBlocked = true;
    return false;
  }

  //// Intention API
  @Override virtual Bool init(Intender & in) {
    print("QHCSIN10");
    if (super.init(in)) return true;
    print("QHCSIN11");
    QHardCell & gc = (QHardCell&) in;
    mHC3State.init(gc);
    print("QHCSIN12");
    return false;
  }

  // @Override virtual Bool observe(Intender & in) 
  @Override
  virtual Bool stabilize(Intender & in) {
    QHardCell & hc = (QHardCell&) in;
    print("QHCSSTAB10");
    if (hc.update(self)) return true;
    print("QHCSSTAB11");
    return false;
  }

  @Override virtual Bool act(Intender & in) {
    QHardCell & hc = (QHardCell&) in;
    print("QHCSACT10");
    if (super.act(hc)) return true;
    if (updatePatch(hc)) return true;
    print("QHCSACT11");
    return false;
  }

  PatchState mPatchState;
  PatchState & getPatchState() { return mPatchState; }

  Bool updatePatch(QHardCell & qhc) {
    print("QSUPTC10");

    constant Unsigned cUPDATE_PATCH_ODDS = 1u; // XXX
    Random random;
    if (!random.oneIn(cUPDATE_PATCH_ODDS))
      return false;

    print("QSUPTC1010");

    //// CAN WE INIT PATCH STATE?
    if (!mPatchState.init(qhc)) return false; // NO WE CANNOT

    //// WE INITTED PATCH STATE

    print("QSUPTC11");

    //// IS THERE A POCKETABLE TO UPDATE?
    if (mPatchState.mPCount > 0u) {
      EventWindow ew;
      Pocketable & pa = (Pocketable&) ew[mPatchState.mPSN];
      print("QSUPTC12");
      print(pa);
      return pa.updatePatch(self, qhc);
    }

    print("QSUPTC13");
    return false;
  }

}

quark QHardCell : Intender
  + QDSHops
  + DEWLine
  + DTU
  + QDebugUtils {

  
  //// Intender API
  @Override
  virtual ClassId getIntentionClass() { return QHardCellState.classidof; }

  //// QHardCell STATE
  NgbRels mNgbRels;

  //// QHardCell API
  typedef EventWindow.SiteNum SN;

  virtual Bool countWatchdog() ;
  virtual Void resetWatchdog() ;

  virtual Bool inMyGroup(QHardCell & qh) { return true; }

  virtual Unsigned getUpHops() ;          // Distance to root
  virtual Void setUpHops(Unsigned uphops) ;

  virtual Unsigned getMaxHopsGoal() ;     // Intended max hops (here)
  virtual Void setMaxHopsGoal(Unsigned maxup) ; 
  
  virtual Unsigned getSettledDownHops() ; // Down hops all aligned with me 
  virtual Void setSettledDownHops(Unsigned settledown) ;

  virtual Bool rootBehave(QHardCellState &hcs) { return false; } 
  virtual Void initCopy() { setUpHops(Unsigned.maxof); /* set uninitted */ }

  virtual Atom toSpawn() ;      //< Concrete subclass to build for spawning

  /*
  typedef Unary(2) ContactWarning;
  constant ContactWarning cCW_OPEN = 0u; //< Nothing visible in direction
  constant ContactWarning cCW_EDGE = 1u; //< Inaccessible ngb beyond maxhops
  constant ContactWarning cCW_DAMG = 2u; //< Inaccessible ngb below maxhops
  typedef ContactWarning ContactWarnings[4];

  virtual ContactWarnings getContactWarnings() ;
  virtual Void setContactWarnings(ContactWarnings cw) ;
  */
  
  //// QHardCell SERVICES
  Bool isTail() { return getUpHops() == getMaxHopsGoal() - 1u; }

  /*
  Bool blockOrDie(Unsigned dieOdds) {
    Random random;
    //XXX JUST BLOCK FOR NOW    if (random.oneIn(dieOdds)) return die();
    return block();
  }
  Bool block() { return true; } // Nothing happened but we need to wait
  */
  Bool done() { return true; }  // Things are different now 
  Bool die() {
    EventWindow ew;
    ew[0] = Empty.instanceof; 
    return true;
  }
  
  //XXX  constant Unsigned cMIN_DOWN_SETTLED = 6u; // Say we must be this settled to ride
  constant Unsigned cMIN_DOWN_SETTLED = 3u; // DEBUG TEST Say we must be this settled to ride

  // \returns true if blocked or things changed, false if bored
  Bool update() {
    QHardCellState qcs;
    return update(qcs);
  }

  Bool update(QHardCellState & hcs) {
    DebugUtils du;
    EventWindow ew;
    Random random;
    C2D origin;

    print("QHCUP10");
    if (countWatchdog()) return die();

    print("QHCUP11");
    //    hcs.init(self);  XXXX DOH IT WAS ALREADY INITTED

    //// NEIGHBORS INITS AND MIN UPHOP 
    HC3State & hc3 = hcs.getHC3State();
    hc3.examineNgbs(self);
    print("QHCUP11NGBS");
    print(hc3);

    /// I KNOW MINUPHOPS AND UNINITS.
    
    //// AM I UNINITTED?
    if (hc3.myUpHops > hc3.myUpHopsGoal) {
      print("QHCUP12");
      // YES
      /// ARE THERE ANY NGBS TO LOOK TO?
      if (hc3.minUpHops >= hc3.myUpHopsGoal) return die(); // NO
      setUpHops(hc3.minUpHops + 1u);
      setDSHops(hc3.minDSHops + 1u);
      setOnDSAxes(hc3.myOnDSAxes);

      return hcs.block();
    }
    //// I AM INITTED
    print("QHCUP13");

    //// DO I NEED TO UPDATE GOAL HOPS?
    if (hc3.myUpHops != 0u) {       // I'M NOT ROOT.
      print("QHCUP14");

      // DIE IF NO NGBS.
      if (hc3.minUpHops == hc3.minUpHops.maxof) return die();

      //// DO I NEED TO UPDATE MAXHOPS?
      if (hc3.myUpHopsGoal != hc3.maxUpHopsGoal) {
        print("QHCUP15");
      
        // I DO.
        print("QHDCL MAXGOALHOPS CHG");
        printContext();
        setMaxHopsGoal(hc3.maxUpHopsGoal);
        setSettledDownHops(0u);  // and downstream is now considered unsettled
        return hcs.block();
      }
    }
    //// MY MAX GOAL HOPS IS CONSISTENT
    print("QHCUP16");

    //// DO I HAVE ANY INITTED NGBS?
    if (hc3.minUpHops != Unsigned.maxof) {
      // I HAVE AT LEAST ONE.
      // AM I BEYOND THE GOAL?
      if (hc3.minUpHops + 1u >= hc3.maxUpHopsGoal) // YES
        return die();
    }

    //// DO I HAVE UNINITTED NGBS?
    if (hc3.uninits)
      return hcs.block();
    //// I HAVE NO UNINITTED NGBS.

    print("QHCUP17");

    //// AM I NOT A DS?
    if (!(self is DiamondSequencer)) {
      setOnDSAxes(hc3.myOnDSAxes); //FOG IT JUST ALWAYS UPDATE AXES??

      //// I AM NOT A DS. DO I NEED TO UPDATE DSHOPS?
      if (hc3.myDSHops != (DSHops) (hc3.minDSHops + 1u)) {
        print("QHCUP1710");
        // I DO.
        setDSHops(hc3.minDSHops + 1u);
        return hcs.block();
      }
    }

    //// AM I ROOT?
    if (hc3.myUpHops != 0u) {
      // I'M NOT ROOT.
      print("QHCUP18");

      // DIE IF NO NGBS.
      if (hc3.minUpHops == hc3.minUpHops.maxof) return die();

      //// DO I NEED TO UPDATE MAXHOPS?
      if (hc3.myUpHopsGoal != hc3.maxUpHopsGoal) {
        // I DO.
        print("QHCUP19");
        setMaxHopsGoal(hc3.maxUpHopsGoal);
        return hcs.block();
      }
      
      //// DO I NEED TO UPDATE UPHOPS?
      if (hc3.myUpHops != hc3.minUpHops + 1u) {
        print("QHCUP210");
        // I DO.
        setUpHops(hc3.minUpHops + 1u);
        return hcs.block();
      }

      print("QHCUP211");
      // LIKELY DIE IF NO ROOM FOR ME.
      if (hc3.minUpHops >= hc3.myUpHopsGoal) return done(); // ?? or die() ??
      // THERE IS ROOM FOR ME.

    }
    //// MY UPHOPS WAS CONSISTENT WITH NGBS
    // (I MAY OR MAY NOT BE ROOT.)
    print("QHCUP212");

    //// DO I HAVE ANY cNGB_MULTI NGBS?
    for (Unsigned i = 0u; i < hc3.mHC3NgbStates.lengthof; ++i) {
      HC3NgbState & n = hc3.mHC3NgbStates[i];
      if (n.ngbState() == HC3NgbState.cNGB_MULTI)
        return hcs.block();
    }
    //// I HAVE NO cNGB_MULTI NGBS.
    print("QHCUP213");

    //// REFRESH MY SETTLEDDOWNHOPS?
    Unsigned mysettleddowns = 0u;
    Unsigned mytotaldowns = 0u;
    for (Unsigned i = 0u; i < hc3.mHC3NgbStates.lengthof; ++i) {
      HC3NgbState & n = hc3.mHC3NgbStates[i];
      if (n.foundAnyDownstream()) {
        ++mytotaldowns;

        if (!n.hasValidNeighbor()) {
          du.print("INVALID SETTLEDDOWNHOPS");
          du.print(i);
          du.print(n);
          n.assertHasNeighbor(); // boom
        }
        NgbState ngbstate = n.ngbState();
        if (ngbstate != cNGB_CENTER ||    // Downstream not settled on us
            n.getMaxGoalHops() != hc3.myUpHopsGoal) { // or disagreement about size
          mysettleddowns = 0u;
          break;
        }
        QHardCell & ngb = n.getNgb();
        Unsigned downsbythis = ngb.getSettledDownHops() + 1u;
        if (mysettleddowns == 0u || mysettleddowns > downsbythis)
          mysettleddowns = downsbythis;
      }
    }
    // mysed == 0 : we have an unaligned down or no downs at all
    // otherwise : all downs aligned at least this far to us
    self.setSettledDownHops(mysettleddowns);
    //// MY SETTLEDDOWNHOPS IS CURRENT
    print("QHCUP214");


    /// UPDATE MY CONTACTWARNINGS IF NEEDED
    DEWLineC dl;
    if (mytotaldowns == 0u) {
      print("QHCUP215");

      /// ..BASED ON OBSERVATIONS, BECAUSE I AM A LEAF (NO DOWNSTREAM?)
      for (Unsigned i = 0u; i < hc3.mHC3NgbStates.lengthof; ++i) {
        HC3NgbState & n = hc3.mHC3NgbStates[i];
        if (n.foundInaccessible() || n.foundBlockage()) {
          dl.maxDC((DirIdx) i,
                   (hc3.myUpHops < hc3.myUpHopsGoal - 1u) ? DEWLine.cDC_DAMG : DEWLine.cDC_CONT);
        }
        /* XXX THIS CAN CAUSE DEADLOCK BY ADDING UNSIGNALED CONTACT DIRS?
           XXX IF ANYTHING SHOULD SAMPLE THE WARNING SITE AND MERGE?
        if (n.foundFoamWarning())
          dl.maxDC((DirIdx) i, DEWLine.cDC_SGHT);
        */
      }
      /// ..OR BY MEMBRANE REPORTS
      for (SN sn = 1u; sn <= 8u; ++sn) {
        if (!ew.isAccessible(sn)) continue;
        Atom & a = ew[sn];
        if (a as Membrane) {
          dl.mergeDCs(a);
        }
      }

    } else {
      print("QHCUP216");
      /// ..OR BY MAXING DOWNSTREAM, BECAUSE I AM NOT A LEAF
      for (Unsigned i = 0u; i < hc3.mHC3NgbStates.lengthof; ++i) {
        HC3NgbState & n = hc3.mHC3NgbStates[i];
        if (n.foundAnyDownstream()) {
          QHardCell & hc = n.getNgb();
          dl.mergeDCs(hc);
        }
      }
    }
    print("QHCUP217");
    if (dl != self) {
      self.copyDEWLine(dl);
      return hcs.block();         // AND QUIT (HELP WARNINGS SPEED AHEAD)
    }
    //// MY CONTACTWARNINGS ARE UNCHANGED

    print("QHCUP218");

    //// AM I TAIL?
    if (hc3.myUpHops < hc3.myUpHopsGoal - 1u) {
      // I'M NOT TAIL
      
      print("QHCUP219");
      //// DO I NEED TO SPAWN?
      Bool spawns = false;
      for (Unsigned i = 0u; i < hc3.mHC3NgbStates.lengthof; ++i) {
        HC3NgbState & n = hc3.mHC3NgbStates[i];
        if (n.isEmpty() &&   // Whole ngb empty
            ew.isAccessible(n.mNgbCtr)) { // ctr is accessible
          setSettledDownHops(0u);  // After I spawn, downstream will be unsettled
          Atom a = toSpawn();      // Get a concete Self subclass to spawn
          print("QHCUP219SPONG");
          print(a);
          Self & copy = (Self&) a;
          copy.initCopy();
          /*

          if (copy as HardCellG) {
            copy.mDirs[0] = 8; // flag values
            copy.mDirs[1] = 7;
            copy.mHardCellWatchdog.time=3;
            copy.mColor[0] = 1;
            copy.mColor[1] = 2;
            copy.mColor[2] = 3;
          }
          copy.mNgbRels[0] = 2;
          copy.mNgbRels[1] = 1;
          copy.mNgbRels[2] = 3;
          copy.mNgbRels[3] = 1;

          print("QHCUP219SPONGCOPY");
          print(copy);
          copy = self;          // Take our QHardCell fields to start
          print("QHCUP219SPONGSELF");
          print(copy);
          print("QHCUP219SPONGINIT");
          print(copy);
          */
          ew[n.mNgbCtr] = a;
          print("QHCUP219DONE");
          print(n.mNgbCtr);
          print(ew[n.mNgbCtr]);
          spawns = true;
        }
      }

      if (spawns) return true; // I SPAWNED.

      print("QHCUP220");

      //// I DID NOT NEED TO SPAWN.
      // (I MAY OR MAY NOT BE TAIL)
    } else {
      print("QHCUP221");
      //// I AM TAIL
      /// XXX HACK RE-SEARCH MOORE NGB
      Unsigned baseCount;
      for (SN sn = 5u; sn <= 8u; ++sn) {
        if (!ew.isAccessible(sn)) continue;
        if (ew[sn] is MBase) 
          ++baseCount;
      }
      if (baseCount < 2u) {
        SN ngb = (SN) random.between(5u,8u); // diago to me
        if (ew[ngb] is Empty) {
          ew[ngb] = MBase.instanceof;
          // XXX NO FOAM RADIO Mon Aug  1 16:44:08 2022:
          //ew[ngb] = Foam.instanceof;
          //return true; // I foamed
        }
      }
    }
    print("QHCUP222");

    /////// CONSIDERATIONS FOR MOVING (ROOT OR NON-ROOT)

    //// DO I HAVE ENOUGH DOWN ALIGNMENT TO MOVE NOW?
    if (mytotaldowns > 0u &&                  // If I have any downs at all (not tail)
        mysettleddowns < cMIN_DOWN_SETTLED &&  // No good if not there yet and
        hc3.myUpHops + mysettleddowns < hc3.myUpHopsGoal - 1u) { // not near the tail
      print("QHCUP223");
      
      ByteStreamLogger bsl;
      /*      bsl.printf("msd %d, mtd %d, myu %d, mxh %d\n",
                 mysettleddowns,
                 mytotaldowns,
                 hcs.myuphops,
                 hcs.maxhop);*/
      return hcs.block(); // NOT SETTLED DOWN ENOUGH TO MOVE
    }
    //// I HAVE AT LEAST cMIN_DOWN_SETTLED DOWN ALIGNMENT OR TO TAIL

    print("QHCUP224");
    //// AM I ROOT?
    if (hc3.myUpHops == 0u)
      return rootBehave(hcs); // YES, HANDLE ME
    //// I AM NOT ROOT.

    print("QHCUP225");
    //// DO MY UPS HAVE A CONSENSUS ALIGNMENT?
    NgbState upstate = cNGB_INIT;
    for (Unsigned i = 0u; i < hc3.mHC3NgbStates.lengthof; ++i) {
      HC3NgbState & n = hc3.mHC3NgbStates[i];
      if (n.foundAnyUpstream()) {
        if (!n.hasValidNeighbor()) {
          du.print("INVALID FOUND UPS");
          n.assertHasNeighbor(); // boom
        }
        NgbState ngbstate = n.ngbState();
        if (upstate == cNGB_INIT) upstate = ngbstate;
        else if (upstate != ngbstate)
          return hcs.block(); // MY UPS ARE NOT ALL ALIGNED WITH EACH OTHER
      }
    }
    //// MY UPS ARE ALL ALIGNED upstate.

    print("QHCUP226");
    //// AM I UNALIGNED WITH upstate?
    if (upstate == 0u) return false; // I (AM NOT ROOT AND I) ALIGN WITH UPSTATE NOTHING TO DO HERE
    //// I AM NOT ALIGNED WITH upstate.

    print("QHCUP227");
    /*
    //// COULD MY DOWNS ALL TOLERATE ME MOVING upstate ?
    for (Unsigned i = 0u; i < mHC3NgbStates.lengthof; ++i) {
      HC3NgbState & n = mHC3NgbStates[i];
      if (n.foundAnyDownstream()) {
        n.assertHasNeighbor(); // WE HAVE A PROPER GRID NGB SOMEWHERE IN HERE

        HC3NgbState.State ngbstate = n.ngbState();
        if (ngbstate != 0u && ngbstate != upstate) // NEED TO WAIT FOR DOWNSTREAM
          return blockOrDie(2000u);
      }
    }
    //// MY DOWNS COULD ALL TOLERATE ME MOVING upstate.
    */

    /// AM I BLOCKED FROM MOVING upstate?
    if (/*!ew.isAccessible(upstate) ||*/
        !ew.isEmpty(upstate))
      return hcs.block(); // I AM BLOCKED.

    /// I AM NOT BLOCKED: upstate IS EMPTY (OR INACCESSIBLE)

    //// I AM GOING TO FOGGEN MOVE. I WILL NOT REMAIN SETTLED.
    setSettledDownHops(0u);     // Full restart for settlements
    for (Unsigned i = 0u; i < hc3.mHC3NgbStates.lengthof; ++i) {
      HC3NgbState & n = hc3.mHC3NgbStates[i];
      if (n.hasValidNeighbor()) {
        Self & qc = n.getNgb();
        qc.setSettledDownHops(0u); // NUKE all down ngb hops
      }
    }
    print("QHCUP228");
    moveEntourage(hcs, upstate); // GO HERE
    //// I MOVED.
    
    return true; // CANNOT SAFELY CALL (eg) done() HERE!
  }

  Void moveEntourage(QHardCellState& qcs, SN destsn) {
    HC3State & hcs = qcs.getHC3State();
    DebugUtils du;
    EventWindow ew;
    C2D dest = ew.getCoord(destsn);
    C2D side(dest.y,dest.x); // swap coords

    //// MARK WE ARE LIVE
    resetWatchdog();

    //// RECOMPUTE NGBRELS AS OF AFTER I MOVE
    {
      ByteStreamLogger bsl;
      //      bsl.printf("rels %d %d %d %d + %d\n",mNgbRels[0],mNgbRels[1],mNgbRels[2],mNgbRels[3],destsn);
      hcs.updateNgbsForMove(destsn);
      hcs.summarizeRels(mNgbRels);
      //      bsl.printf("rels %d %d %d %d\n",mNgbRels[0],mNgbRels[1],mNgbRels[2],mNgbRels[3]);
    }

    // Advance the entourage
    for (C2D.Coord flip = -1; flip < 2; flip += 2) {
      C2D sideout = side*flip;
      C2D fmover = dest + sideout;

      if (!(ew[fmover] is Empty)) { // Need to advance front bein
        C2D mdest = dest*2 + sideout;
        if (!(ew[mdest] is Empty)) { // Need to push
          C2D altdest = mdest + sideout;
          if (!(ew[altdest] is Empty)) {
            DebugUtils du;
            //du.print("SMASH OUT");
            //du.print(ew[altdest]);
          }
          ew[altdest] = ew[mdest]; // pushed
        }
        ew[mdest] = ew[fmover];
        ew[fmover] = Empty.instanceof;
      }

      C2D bmover = dest*-1 + sideout;
      if (!(ew[bmover] is Empty)) { // Need to advance back bein
        C2D bdest = bmover + dest;
        ew[bdest] = ew[bmover];
        ew[bmover] = Empty.instanceof;
        
      }
    }

    // Lastly, advance self
    ew.swap(0u,destsn);
  }

}
