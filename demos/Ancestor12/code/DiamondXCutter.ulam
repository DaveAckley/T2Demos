transient DiamondXCutterBase : PocketableState + QDebugUtils {
  @Override
  virtual Bool init(Intender & in) {
    if (super.init(in)) return true;
    DiamondXCutter & dc = (DiamondXCutter &) in;
    takeDebug(dc);
    return false;
  }

  @Override
  virtual Bool observe(Intender & in) {
    if (super.init(in)) return true;
    findAvailables();
    return false;
  }

  Bool die() {
    print("DCYING");
    EventWindow ew;
    ew[0] = Empty.instanceof;
    return true;
  }

  /** \returns non-zero C2D of empty site in uncrowded adjacent pocket
      ad, or (0,0) if cannot find such a site
   */
  C2D couldMoveAD(AxialDir ad) {
    C2D origin; // (0,0) means can't move ad

    RectIterator ri;
    mPocket.takeDebug(self);
    if (!mPocket.initIterator(ri, (SN) (ad + 1u), true)) return origin;
    print("DCTMAD11");
    Unsigned emptycount, fullcount;
    C2D emptyc;
    Random random;
    while (ri.hasNext()) {
      C2D c = ri.next();
      if (!ew.isAccessible(c)) continue;
      Atom & a = ew[c];
      if (a is Empty && random.oneIn(++emptycount))
        emptyc = c;
      else ++fullcount;
    }
    print("DCTMAD12");
    print(fullcount);
    print(emptycount);
    if (fullcount > 1u || emptycount == 0u) return origin;
    print("DCTMAD13");
    return emptyc;
  }

  Bool tryMoveAD(DiamondXCutter & dc, AxialDir ad) {
    print("DCTMAD10");
    C2D destc = couldMoveAD(ad);
    if (destc.length() == 0u) return false;
    print("DCTMAD11");
    ew.swap(C2D.instanceof, destc);
    return true;
  }

  Bool rootAdjacent(PocketDir & dirquad) {
    PocketHC3 & p = mPocket;
    Unsigned mincorneridx;
    Unsigned mincornerhops = Unsigned.maxof;
    Unsigned mincornercount = 0u;
    SN mincornerSN;
    Random random;
    for (Unsigned i = 0u; i < p.mCornerHops.lengthof; i++) {
      if (p.mCornerHops[i] < mincornerhops) {
        mincorneridx = i;
        mincornerhops = p.mCornerHops[i];
        mincornercount = 1u;
      } else if (p.mCornerHops[i] == mincornerhops) {
        if (i/2u > mincorneridx/2u || // Favor downward dirs to get below target
            random.oneIn(++mincornercount)) {
          mincorneridx = i;
        }
      }
    }

    print("DCFOR11");
    if (mincornercount == 0u) return die(); // wth?
    dirquad = (PocketDir) mincorneridx; // best dir we found
    return (mincornerhops == 0u);
  }

  /** \returns pocketdir of dsroot if found else unsigned maxof, also
     sets nextad in any case
   */
  Unsigned diamondSequencerAdjacent(AxialDir & nextad, AxialDir lastad) {
    print("DCDSA10");
    PocketHC3 & p = mPocket;
    Unsigned mincorneridx;
    Unsigned mincornerdshops = Unsigned.maxof;
    Unsigned mincornercount = 0u;
    SN mincornerSN;
    Random random;
    for (Unsigned i = 0u; i < p.mCornerHops.lengthof; i++) {
      if (p.mCoords[i].length() == 0u) continue; // XXX?
      QDSHopsStg qdsh = p.mCornerQDSHopsStg[i];
      Unsigned dshops = qdsh.getDSHops();
      if (dshops < mincornerdshops) {
        print("DCDSA10BEAT");
        mincorneridx = i;
        mincornerdshops = dshops;
        mincornercount = 1u;
        print(mincorneridx);
        print(mincornerdshops);
      } else if (dshops == mincornerdshops) {
        if (i/2u > mincorneridx/2u || // break ties southward
            random.oneIn(++mincornercount)) {
          print("DCDSA10TIE");
          mincorneridx = i;
          print(mincorneridx);
        }
      }
    }

    print("DCDSA11");
    if (mincornercount == 0u) die(); // wth?

    nextad = lastad;            // assume keep tracking
    if (mincornerdshops == 0u) 
      return mincorneridx;      // ds root found

    print("DCDSA12");

    constant Unsigned cDSHOPS_GRADIENT_THRESHOLD = 5u;
    if (mincornerdshops < cDSHOPS_GRADIENT_THRESHOLD) {
      print("DCDSA13");
      print(mincornerdshops);
      
      AxialDir ad1, ad2;
      axialDirsBorderingQuad((PocketDir) mincorneridx, ad1, ad2);
      nextad = random.oneIn(2) ? ad1 : ad2;
    }

    print("DCDSA14");
    return Unsigned.maxof;    // nextad set or defaulted
    
  }
}

/**  - DC intention 0:
       = Find root
       = Derive config info from it
       = Release DC intention 1s toward new roots
       = adopt intention 2
 */
transient DiamondXCutterFindOldRoot : DiamondXCutterBase {
  @Override
  virtual Bool act(Intender & in) {
    PocketHC3 & p = mPocket;
    DiamondXCutter & dc = (DiamondXCutter &) in;

    print(__FILE__,__LINE__);
    print(self);
    print(dc);

    //// ARE WE ROOT ADJACENT?
    print("DCFOR10");
    PocketDir towardroot;
    Bool rootadjacent = rootAdjacent(towardroot);

    if (!rootadjacent) {

      //// WE ARE NOT ROOT ADJACENT BUT IT'S THATAWAY
      AxialDir ad1, ad2;
      axialDirsBorderingQuad(towardroot, ad1, ad2);
      print("DCFOR12");
      return tryMoveAD(dc, random.oneIn(2u) ? ad1 : ad2);
    }

    //// WE ARE ROOT ADJACENT.
    print("DCFOR13");

    return deployRootSeeds(dc, p.mCoords[towardroot]);
  }

  Bool deployRootSeeds(DiamondXCutter & dc, C2D rootc) {
    Fail f;
    print("DCFORDRS11");
    //// GET ACCESS TO THE ROOT
    EventWindow ew;
    if (!ew.isAccessible(rootc)) f.fail(__FILE__,__LINE__);
    Atom & a = ew[rootc];
    QHardCell & qhc = (QHardCell&) ew[rootc]; // or fail
    if (qhc.getUpHops() != 0u) f.fail(__FILE__,__LINE__);
    //// GOT ACCESS TO THE ROOT

    //// FOR NOW AT LEAST, INSIST THE ROOT IS HARDCELLG
    HardCellG & rootg = (HardCellG &) qhc; // or fail

    //// TAKE IT OVER
    rootg.mCmdPhase = HardCellG.cCMD_WAIT;

    //// IS THE GRID ALL SETTLED?
    if (!rootg.stableToEdge()) return true; // no, block
    //// THE GRID IS ALL SETTLED

    //// CONFIGURE PARAMETERS
    Unsigned oldmaxgoal = qhc.getMaxHopsGoal();
    dc.mNewMaxGoalHops = (HardCellG.HopCount) (oldmaxgoal/2u-0u); // we shall see

    DiamondXCutter.AxialDirMap adm = dc.getNewRootDirs(self);
    //// CAN WE DEPLOY IN ALL NEEDED DIRS
    C2D dests[4];
    for (Unsigned i = cW; i <= cE; ++i) {
      AxialDir ad = (AxialDir) i;
      if ((adm & (1u<<ad)) != 0u) {
        //// CAN WE DEPLOY TOWARD ad
        dests[i] = couldMoveAD(ad);
        if (dests[i].length() == 0u) return false; // NO
        //// WE COULD DEPLOY TOWARD ad
      }
    }
    //// WE CAN DEPLOY IN ALL NEEDED DIRS

    //// HAVE WE FULLY DEPLOYED
    for (Unsigned i = cW; i <= cE; ++i) {
      AxialDir ad = (AxialDir) i;
      if ((adm & (1u<<ad)) != 0u) {
        Atom copy = dc.atomof;  // newmaxgoal already configured
        DiamondXCutter & dcnew = (DiamondXCutter&) copy;
        dcnew.mStateIdx = DiamondXCutter.cDCS_SEED_NEW_ROOT;
        dcnew.mOriginalAxialDir = ad;
        dcnew.mEventCounter = dcnew.mEventCounter.maxof; // Last ditch
        ew[dests[i]] = dcnew.atomof; // THE BIRD IS AWAY
      }
    }
    //// WE HAVE FULLY DEPLOYED;
    dc.mStateIdx = dc.cDCS_DELETE_OLD_ROOT; // Adopt a new intention
    dc.mEventCounter = (DiamondXCutter.EventCounter) (1u*oldmaxgoal); // set timer

    return true;
  }
  
}

/**  - DC intention 1:
       = Climb dshops gradient.
       = If no gradient, continue in same direction
       = when can see DS and HG south of DS, convert
         HG south of DS to a configured root and erase 
         self
 */
transient DiamondXCutterSeedNewRoot : DiamondXCutterBase {
  @Override
  virtual Bool act(Intender & in) {
    DiamondXCutter & dc = (DiamondXCutter &) in;
    EventWindow ew;

    //// DO WE HAVE MORE TIME?
    if (dc.mEventCounter-- == 0u) {
      ew[0] = Empty.instanceof;
      return true;
    }
    //// WE DO HAVE MORE TIME

    //// ARE WE DS ADJACENT?
    print("DCSNR10");
    PocketDir towardds;
    AxialDir nextad;
    Unsigned dspd = diamondSequencerAdjacent(nextad, dc.mOriginalAxialDir);
    if (dspd == Unsigned.maxof) {
      //// WE ARE NOT DS ADJACENT
      print("DCSNR11");
      return tryMoveAD(dc, nextad);
    }

    //// WE ARE DS ADJACENT.
    print("DCSNR12");
    Unsigned newrootpd;
    if (dspd == cQUAD_NW) newrootpd = cQUAD_SW;
    else if (dspd == cQUAD_NE) newrootpd = cQUAD_SE;
    else return tryMoveAD(dc,cS);  // BUT NOT BELOW DS: TRY GOING S

    print("DCSNR13");
    PocketHC3 & p = mPocket;

    Atom & a = ew[p.mCoords[newrootpd]];
    QHardCell & newroot = (QHardCell&) a; // or fail

    if (newroot as HardCellG)
      newroot.mCmdPhase = newroot.cCMD_STOP; // START OUT STOPPED

    newroot.setUpHops(0u);                      // YOU ARE ROOT
    newroot.setMaxHopsGoal(dc.mNewMaxGoalHops); // OVER ALL THIS LAND
    ew[0] = Empty.instanceof;                   // AND MY JOB IS DONE

    return true;
  }
}

/**  - DC intention 2:
       = Count newmaxhopsgoal events
       = Destroy local root by setting its mHops = 1u
       = Erase self
*/
transient DiamondXCutterDeleteOldRoot : DiamondXCutterBase {
  @Override
  virtual Bool act(Intender & in) {
    Fail f;
    PocketHC3 & p = mPocket;

    DiamondXCutter & dc = (DiamondXCutter &) in;
    if (dc.mEventCounter-- == 0u) {

      //// TIME TO NUKE THE ROOT
      PocketDir rootpd;
      Bool atroot =  rootAdjacent(rootpd);
      if (!atroot) f.fail(__FILE__,__LINE__);
      C2D rootc = p.mCoords[rootpd];
      EventWindow ew;
      Atom & a = ew[rootc];
      if (!(a is QHardCell)) f.fail(__FILE__,__LINE__);
      {
        Split s;
        s.mHorizontal=false;
        s.mPosition = 1u;
        ew[rootc] = s.atomof;
      }
      /* TRY SPLITTING AT THE OLD ROOT INSTEAD
      QHardCell & qhc = (QHardCell&) a;
      qhc.setUpHops(1u); // BUH BYE
      //// THE ROOT HAS BEEN NUKED
      */
      ew[0] = Empty.instanceof;
      //// AS HAVE WE
      return true;
    }

    print(__FILE__,__LINE__);
    print(dc.mNewMaxGoalHops);
    return false;
  }
}

quark DiamondXCutter : Pocketable + DTU {
  //// DiamondXCutter API
  typedef Bits(AxialDir.maxof + 1u) AxialDirMap;

  /** Which directions to deploy new roots. 
   */
  virtual AxialDirMap getNewRootDirs(DiamondXCutterBase & dcb) ;

  typedef Unary(3) StateIdx;
  constant StateIdx cDCS_FIND_OLD_ROOT = 0u;
  constant StateIdx cDCS_SEED_NEW_ROOT = 1u;
  constant StateIdx cDCS_DELETE_OLD_ROOT = 2u;

  StateIdx mStateIdx = cDCS_FIND_OLD_ROOT;

  AxialDir mOriginalAxialDir; // which way did we start out
  HardCellG.HopCount mNewMaxGoalHops; // also event counter in DC#3?
  typedef Unsigned(HardCellG.HopCount.sizeof + 4u) EventCounter; // Four bits bigger than HopCount..
  EventCounter mEventCounter;

  //// INTENDER API
  @Override
  virtual ClassId getIntentionClass() {
    which (mStateIdx) {
      case cDCS_FIND_OLD_ROOT:  { return DiamondXCutterFindOldRoot.classidof; }
      case cDCS_SEED_NEW_ROOT:  { return DiamondXCutterSeedNewRoot.classidof; }
    }
    /* cDCS_DELETE_OLD_ROOT: */ { return DiamondXCutterDeleteOldRoot.classidof; }
  }
  
}

/** CutDiamondVertically
    \symbol XV
    \color #00f
 */
element XCutDiamondVertically : DiamondXCutter {
  @Override
  virtual AxialDirMap getNewRootDirs(DiamondXCutterBase & dcb) {
    return (AxialDirMap) ((1u<<cW)|(1<<cE));
  }

  Void behave() {
    pursueIntention();
  }
}

