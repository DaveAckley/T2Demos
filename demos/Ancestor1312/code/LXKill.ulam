/** Kill and perhaps init pockets, with optional regrip and
    optional killing of the DS

    \symbol KL 
    \color #e42
 */
element LXKill : LX + QLabelMark {
  typedef Bool PocketMap[4];
  PocketMap mPocketsToKill;
  PocketMap mPocketsToInit;
  ProcUnit mRegripXToPU = cPUNIT_XEQ; //< default means don't regrip
  Bool mKillDS;

  typedef Unary(2) KillPhase;
  constant KillPhase cKP_KILL = 0u;
  constant KillPhase cKP_INIT = 1u;
  constant KillPhase cKP_WRAP = 2u;

  KillPhase mKillPhase = cKP_KILL;

  //// LX API
  @Override
  virtual Bool perform(DiamondSequencerState & dss) {
    EventWindow ew;
    BaseClassUtils bcu;
    Fail f;

    print("LXKILL10");
    /// ARE ALL DOOMED POCKETS (EXCEPT XEQ) KILLED?
    if (mKillPhase == cKP_KILL) {
      print("LXKILL11");
      Bool killingInProgress;
      for (Unsigned i = cPUNIT_REG; i < cPUNIT_XEQ; ++i) { // ALL BUT XEQ
        ProcUnit pu = (ProcUnit) i;
        if (!mPocketsToKill[pu]) continue;  // leave that one be
        // WANT KILL
        Bool killed = killPU(pu, dss);
        if (!killed) killingInProgress = true;
        print("LXKILL12");
        print(getProcUnitName(pu));
        print(killed);
      }
      if (killingInProgress) return true;
      print("LXKILL13");
      mKillPhase = cKP_INIT;
      // FALL THROUGH
    }
    /// ALL DOOMED POCKETS (EXCEPT XEQ) ARE KILLED

    print("LXKILL14");
    /// ARE NEWBORNE POCKETS (EXCEPT XEQ) INITTED?
    if (mKillPhase == cKP_INIT) {
      print("LXKILL15");

      for (Unsigned i = cPUNIT_REG; i < cPUNIT_XEQ; ++i) { // ALL BUT XEQ
        ProcUnit pu = (ProcUnit) i;
        if (!mPocketsToInit[pu]) continue; // leave that one be
        // WANT INIT

        ProcUnitInfo & pui = dss.getPUInfo(pu);

        print("LXKILL16");
        print(getProcUnitName(pu));

        if (pui.mOccupiedSites == 0u) { // Init pu if empty
          if (pui.mEmptySN == SN.maxof) f.fail(__FILE__,__LINE__); // WTF?
          print("LXKILL17");
          print(pui.mEmptySN);
          Looper looper;
          bcu.copySharedData(self,looper);
          looper.init(); // init over shared data
          ew[pui.mEmptySN] = looper.atomof; // OK, it's not empty now
          print("LXKILL18");
        }
      }
      print("LXKILL19");
      mKillPhase = cKP_WRAP;
      // FALL THROUGH
    }
    /// NEWBORNE POCKETS (EXCEPT XEQ) ARE INITTED

    print("LXKILL210");

    /// IS FINAL WRAP-UP DONE?
    if (mKillPhase == cKP_WRAP) {

      print("LXKILL211");

      //// NEED TO REGRIP FOR THE FUTURE?
      if (mRegripXToPU != cPUNIT_XEQ) {
        print("LXKILL212");
        print(mRegripXToPU);
        if (!dss.swapPUs(mRegripXToPU, cPUNIT_XEQ))
          f.fail(__FILE__,__LINE__);
      }
      //// WE REGRIPPED FOR THE FUTURE.

      print("LXKILL213");

      //// NEED TO KILL DS?
      if (mKillDS) {
        print("LXKILL214");
        ew[0] = Empty.instanceof;
      }
      //// DS IS KILLED IF REQUESTED

      print("LXKILL215");

      //// NEED TO START KILLING SELF?
      if (mPocketsToKill[cPUNIT_XEQ]) {

        print("LXKILL216");

        //// NEED TO START KILLING SELF.
        SN oursn = ew.getSiteNumber(self);
        if (oursn == SN.maxof) f.fail(__FILE__,__LINE__);
        print(oursn);

        LoopKiller lk;
        lk.copySegAndPChain(self);
        lk.mDSType = cDS_DUMB;
        lk.mKillFunc = lk.cKLFN_ROOT;            // We are kill anchor; last to go

        print("LXKILL217");
        print(lk);
        ew[oursn] = lk.atomof;                   // self is STALE
        return true;
      }
      //// DO NOT NEED TO START KILLING SELF

      print("LXKILL218");
      if (!mKillDS) {                              // cannot move on if DS is gone
        print("LXKILL219");        
        markCurrentInstructionForRotation(dss, 0u); // So just move on
      }
      print("LXKILL220");        
      return true;
    }
    f.fail(__FILE__,__LINE__);
    return false; // NOT REACHED
  }

  //// CODON API
  virtual Void init() {
    super.init();
    mDSType = cDS_NORM;
  }

}
