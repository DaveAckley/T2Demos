/** 
    A fully-bounded pocket has four HC3 atoms, one in each ew quadrant
    and none directly on an axis.
*/
transient PocketHC3 {
  typedef EventWindow.SiteNum SN;

  C2D mCoords[4]; // 0 --, 1 +-, 2 ++, 3 -+
  Unsigned mCoordCount;

  typedef Int(16) Bound;
  Bound mBounds[4]; 
  constant Unsigned cMINY = 0u;
  constant Unsigned cMAXX = 1u;
  constant Unsigned cMAXY = 2u;
  constant Unsigned cMINX = 3u;

  typedef Unsigned(2) Status;
  constant Status cSTATUS_OK =   0u;     //< Properly-bounded pocket found
  constant Status cSTATUS_EDGE = 1u;     //< No pocket: Pocket on axis
  constant Status cSTATUS_OPEN = 2u;     //< No pocket: At least one corner missing
  constant Status cSTATUS_BENT = 3u;     //< No pocket: Unaligned adjacent corners or unitted
  Status mStatus = cSTATUS_BENT;

  PocketHC3_CornerInfo mCornerInfo[4];

  /** Init ri to iterate over the pocket */
  Bool initIterator(RectIterator & ri) { return initIterator(ri, 0u, false); }

  /** Init ri to iterate over the pocket or an adjacent side */
  Bool initIterator(RectIterator & ri, SN side, Bool full) { /* 0: self, 1..4: WNSE */
    typedef C2D.Coord C;
    if (mStatus != cSTATUS_OK) return false;
    if (full) return initFullSide_(ri, side);
    which (side) {
      case 0u: { // THE POCKET ITSELF
        ri.init(C2D.instanceof(mBounds[cMINX],mBounds[cMINY]),
                C2D.instanceof(mBounds[cMAXX],mBounds[cMAXY]));
      }
      case 1u: { // ADJACENT WEST SIDE
        ri.init(C2D.instanceof((C) (mBounds[cMINX]-2),mBounds[cMINY]),
                C2D.instanceof((C) (mBounds[cMINX]-1),mBounds[cMAXY]));
      }
      case 2u: { // ADJACENT NORTH SIDE
        ri.init(C2D.instanceof(mBounds[cMINX],(C) (mBounds[cMINY]-2)),
                C2D.instanceof(mBounds[cMAXX],(C) (mBounds[cMINY]-1)));
      }
      case 3u: { // ADJACENT SOUTH SIDE
        ri.init(C2D.instanceof(mBounds[cMINX],(C) (mBounds[cMAXY]+1)),
                C2D.instanceof(mBounds[cMAXX],(C) (mBounds[cMAXY]+2)));
      }
      case 4u: { // ADJACENT EAST SIDE
        ri.init(C2D.instanceof((C) (mBounds[cMAXX]+1),  mBounds[cMINY]),
                C2D.instanceof((C) (mBounds[cMAXX]+2),mBounds[cMAXY]));
      }
      otherwise: { Fail f; f.fail("UNDEFINED SIDE"); }
    }
    return true;
  }

  /** \returns cSTATUS_OK if fully-bounded pocket found.
      
      A fully-bounded pocket has four HC3 atoms, one in each ew
      quadrant and none directly on an axis.
   */
  Status init() {
    DebugUtils du;
    EventWindow ew;
    Unsigned boundCount = 0u;
    for (SN sn = 0u; sn < 41u; ++sn) {
      Atom & a = ew[sn];
      if (a as QHardCell) {
        PocketHC3_CornerInfo ci;
        ci.mNgbRels = a.mNgbRels;
        C2D c = ew.getCoord(sn);
        if (c.x == 0 || c.y == 0) return mStatus = cSTATUS_EDGE;
        else if (c.x < 0 && c.y < 0) update_(0u,c,ci);
        else if (c.x > 0 && c.y < 0) update_(1u,c,ci);
        else if (c.x > 0 && c.y > 0) update_(2u,c,ci);
        else /* c.x < 0 && c.y > 0*/ update_(3u,c,ci);
      }
      if (mCoordCount >= 4u) break;
    }
    if (mCoordCount < 4u) return cSTATUS_OPEN;
    for (Unsigned i = 0u; i < 4u; ++i) {
      Unsigned n = (i+1u)%4u;
      Bool yaxis = (i&1)==0u;
      if (yaxis) {
        if (mCoords[i].y != mCoords[n].y) return mStatus = cSTATUS_BENT;
        mBounds[i] = mCoords[i].y;
      } else {
        if (mCoords[i].x != mCoords[n].x) return mStatus = cSTATUS_BENT;
        mBounds[i] = mCoords[i].x;
      }
    }

    //    mNext = mCoords[0] + C2D.instanceof(1,1);
    return mStatus = cSTATUS_OK;
  }

  // internal
  Void update_(Unsigned(2) idx, C2D coord, PocketHC3_CornerInfo ci) {
    if (mCoords[idx].length() == 0u) {
      ++mCoordCount;
      mCoords[idx] = coord;
      mCornerInfo[idx] = ci;
    }
  }

  Bool initFullSide_(RectIterator & ri, SN side) {
    typedef C2D.Coord C;
    if (side == 0u) return false; // center is not a side pocket
    which (side) {
      case 1u: { // WEST SIDE: ci 0 & 3, ngb 0
        C rel1 = mCornerInfo[0].mNgbRels[0];
        C rel2 = mCornerInfo[3].mNgbRels[0];
        if (rel1 == 0u || rel1 != rel2) return false; // incomplete or bent
        ri.init(C2D.instanceof((C) (mBounds[cMINX]-rel1-1),mBounds[cMINY]),
                C2D.instanceof((C) (mBounds[cMINX]),mBounds[cMAXY]));
      }
      case 2u: { // NORTH SIDE: ci 0 & 1, ngb 1
        C rel1 = mCornerInfo[0].mNgbRels[1];
        C rel2 = mCornerInfo[1].mNgbRels[1];
        if (rel1 == 0u || rel1 != rel2) return false; // incomplete or bent
        ri.init(C2D.instanceof(mBounds[cMINX],(C) (mBounds[cMINY]-rel1-1)),
                C2D.instanceof(mBounds[cMAXX],(C) (mBounds[cMINY])));
      }
      case 3u: { // SOUTH SIDE: ci 2 & 3, ngb 2
        C rel1 = mCornerInfo[2].mNgbRels[2];
        C rel2 = mCornerInfo[3].mNgbRels[2];
        if (rel1 == 0u || rel1 != rel2) return false; // incomplete or bent
        ri.init(C2D.instanceof(mBounds[cMINX],(C) (mBounds[cMAXY])),
                C2D.instanceof(mBounds[cMAXX],(C) (mBounds[cMAXY]+rel1+1)));
      }
      case 4u: { // EAST SIDE: ci 1 & 2, ngb 3
        C rel1 = mCornerInfo[1].mNgbRels[3];
        C rel2 = mCornerInfo[2].mNgbRels[3];
        if (rel1 == 0u || rel1 != rel2) return false; // incomplete or bent
        ri.init(C2D.instanceof((C) (mBounds[cMAXX]),     mBounds[cMINY]),
                C2D.instanceof((C) (mBounds[cMAXX]+rel1+1),mBounds[cMAXY]));
      }
    }
    if (false) {
      ByteStreamLogger bsl;
      bsl.printf("iFS %d (%d,%d) - (%d,%d)\n",
                 side,
                 ri.mMin.x, ri.mMin.y,
                 ri.mMax.x, ri.mMax.y);
    }
    return true;
  }
}

transient PocketHC3_CornerInfo {
  typedef QHardCell.NgbRels NgbRels;
  NgbRels mNgbRels;
}
