transient MembraneState : PocketableState {
}

transient MembraneBaseState : MembraneState {
}

/**
   \symbol MB
   \color #00f
 */
element MBase : Membrane + Random {
  
  virtual ClassId getIntentionClass() {
    return MembraneBaseState.classidof;
  }

  //// UrSelf API
  @Override
  virtual ARGB getColor(Unsigned selector) {
    ARGB ret = super.getColor(selector);
    if (maxDCs() == 0u) {
      ColorUtils cu;
      ret = cu.dimmer(ret);
    }
    return ret;
  }    

  //// DEWLine API
  @Override
  virtual DefCon signalDC() {
    return cDC_CONT; // MBase signals 'contact'
  }

  //// Membrane API

  Void behave() {
    DebugUtils du;
    MembraneBaseState mbs;
    mbs.init(self);
    PocketHC3 & p = mbs.mPocket;
    if (p.mStatus == PocketHC3.cSTATUS_BENT) return; //block
    if (p.mStatus == PocketHC3.cSTATUS_EDGE) return; //block
    if (p.mStatus != PocketHC3.cSTATUS_OPEN) {
      return die();
    }
    // Pocket can see just 2 HC3 during movement
    if (p.mCoordCount == 2u) return; //block
    if (p.mCoordCount != 3u)
      if (oneIn(20u)) return die();
      else return; // block

    /// Find open dir
    Unsigned opendir = Unsigned.maxof;
    for (Unsigned i = 0u; i < 4u; ++i) {
      if (p.mCoords[i].length() == 0u) { // uninitted
        opendir = i;
        break;
      }
    }
    /// Lock or check open dir
    if (mDirBound) {
      if (opendir != mOpenDir) return die();
    } else {
      mOpenDir = (PocketDir) opendir;
      mDirBound = true;
    }
    /// We are bound and good

    Unsigned sensorcount = scanOuterQuadrant();

    // Slow and ungainly sensor seeding
    if (sensorcount == 0u) {
      // Signal contact if we can't see any sensors!
      DirIdx d1, d2;
      axesOf(mOpenDir,d1,d2);
      DefCon dc = signalDC();
      maxDC(d1,dc);
      maxDC(d2,dc);

      if (oneIn(10u)) { 
        SN maybe = (SN) between(5u,24u/*12u*/);
        if (ew.isAccessible(maybe)) {
          if (ew[maybe] is Empty) {
            C2D mc = ew.getCoord(maybe);
            if (inQuadrant(mc,mOpenDir)) {
              MSensor seed;
              seed.mOpenDir = mOpenDir;
              seed.mDirBound = true;
              ew[maybe] = seed.atomof;
            }
          }
        }
      }
    }
  }
}

